plugins {
    id 'kotlin-multiplatform'
    id 'com.android.library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot'

kotlin {
    android {
        publishAllLibraryVariants()
    }
    jvm()
    ios()
    iosArm32('iosArm32')
    tvos()
    js {
        browser()
        nodejs()
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi')
            }
        }

        commonMain {
            dependencies {
                implementation "com.mirego.trikot:streams:$trikot_streams_version"
                implementation "com.mirego.trikot:trikotFoundation:$trikot_foundation_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
            }
        }

        jvmMain {
            dependsOn commonMain
        }

        jsMain {
            dependsOn commonMain
        }

        androidMain {
            dependsOn commonMain
        }

        nativeMain {
            dependsOn(commonMain)
        }

        iosArm32Main {
            dependsOn(nativeMain)
        }

        iosArm64Main {
            dependsOn(nativeMain)
        }

        iosX64Main {
            dependsOn(nativeMain)
        }

        tvosArm64Main {
            dependsOn(nativeMain)
        }

        tvosX64Main {
            dependsOn(nativeMain)
        }
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']
    updateVersionPart = 2
}
